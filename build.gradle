plugins {
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.35.0'
    id 'com.diffplug.spotless' version '8.0.0'
}

group = 'edu.apu.pssdk'
version = '3.0.0'
apply from: "${rootDir}/repositories.gradle"

repositories {
    mavenCentral()
    giteaIntdevRepo(it)
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates(project.group, project.name, project.version)

    pom {
        name = project.name
        description = 'Integrate with Peoplesoft Component Interfaces with Node.js and Python using GraalVM'
        url = 'https://github.com/azusapacificuniversity/pssdk'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'enov'
            name = 'Samuel Demirdjian'
            url = 'https://github.com/enov'
          }
          developer {
            id = 'megapixel99'
            name = 'seth wheeler'
            url = 'https://github.com/megapixel99'
          }
        }
        scm {
          url = 'https://github.com/azusapacificuniversity/pssdk'
          connection = 'scm:git:git://github.com/azusapacificuniversity/pssdk.git'
          developerConnection = 'scm:git:ssh://github.com/azusapacificuniversity/pssdk.git'
        }
    }
}

dependencies {
    implementation 'org.graalvm.polyglot:polyglot:25.0.1'
    implementation 'org.slf4j:slf4j-api:2.0.17'

    compileOnly    'com.oracle.peoplesoft:psjoa:8.60.+'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named("compileJava") {
    dependsOn("spotlessApply")
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

spotless {
    java {
        googleJavaFormat()
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.group + ':' + project.name,
            'Implementation-Version': project.version
        )
    }
}
