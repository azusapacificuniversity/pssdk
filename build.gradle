plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id("com.diffplug.spotless") version "7.2.1"
}

group = 'edu.apu.pssdk'
version = '1.0.0'
apply from: "${rootDir}/repositories.gradle"

repositories {
    mavenCentral()
    giteaIntdevRepo(it)
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
    repositories {
        giteaIntdevRepo(it)
    }
}

dependencies {
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'org.graalvm.polyglot:polyglot:24.2.2'
    implementation 'org.slf4j:slf4j-api:2.0.17'

    compileOnly    'com.oracle.peoplesoft:psjoa:8.60.+'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named("compileJava") {
    dependsOn("spotlessApply")
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

spotless {
    java {
        googleJavaFormat()
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.group + ':' + project.name,
            'Implementation-Version': project.version
        )
    }
}
