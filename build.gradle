plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id("com.gradleup.shadow") version "9.0.2"
    id("com.diffplug.spotless") version "7.2.1"
}

repositories {
    mavenCentral()
    maven {
        name = "gitea-intdev"
        url = "https://gitea.apu.edu/api/packages/intdev/maven"
        credentials {
            username = "intdev"
            password = "$System.env.MVN_TOKEN"
        }
    }
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'org.graalvm.polyglot:polyglot:24.2.2'
    implementation 'com.oracle.peoplesoft:psjoa:8.60.20'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

testing {
    suites { 
        test { 
            useJUnitJupiter() 
        }

        integrationTest(JvmTestSuite) { 
            dependencies {
                implementation project() 
            }

            targets { 
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') { 
    dependsOn(testing.suites.integrationTest)
}

spotless {
  java {
    googleJavaFormat()
  }
}
